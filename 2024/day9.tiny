use import("util.tiny")

data := read_file("inputs/day9.txt")

inp := split_by_char(data, '\n')[0]

cur_id := 0

blocks := aint()

first_free_idx := -1

for i := 0; i < inp->strlen(); i += 1 {
    val := inp[i] - '0'

    if i % 2 == 0 {
        // Files
        for j := 0; j < val; j += 1 {
            blocks->aint_push(cur_id)
        }

        cur_id += 1
    } else {
        if first_free_idx == -1 {
            first_free_idx = blocks->aint_len()
        }

        // Free spaces
        for j = 0; j < val; j += 1 {
            blocks->aint_push(-1)
        }
    }
}

// Collapse
for i = blocks->aint_len() - 1; i >= 0; i -= 1 {
    val = blocks[i]

    if val < 0 {
        continue
    }

    if first_free_idx > i {
        break
    }

    blocks[i] = -1
    blocks[first_free_idx] = val

    for j = first_free_idx + 1; j < blocks->aint_len(); j += 1 {
        if blocks[j] == -1 {
            first_free_idx = j
            break
        }
    }
}

printf("%q\n", blocks)

csum := 0

for i = 0; i < blocks->aint_len(); i += 1 {
    if blocks[i] < 0 {
        break
    }

    csum += blocks[i] * i
}

printf("csum=%q\n", csum)

