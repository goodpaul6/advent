use import("util.tiny")

data := read_file("inputs/day3.txt")

func scan_for_muls(inp: str): int {
    sum := 0
    valid_count := 0
    mul_count := 0
    mul_enabled := true

    for i := 0; i < inp->strlen() - 4; i += 1 {
        ss := substr(inp, i, i + 4)
        
        if ss != "mul(" {
            if ss == "do()" {
                mul_enabled = true
            }

            if i < inp->strlen() - 7 && substr(inp, i, i + 7) == "don't()" {
                mul_enabled = false
            }
            continue
        }
    
        mul_count += 1

        // Scan forward till the close paren, if any
        paren_idx := -1

        for j := i + 5; j < inp->strlen(); j += 1 { 
            if inp[j] == ')' {
                paren_idx = j
                break
            }
        } 

        if paren_idx < 0 {
            continue
        }

        mul_args_str := substr(inp, i + 4, paren_idx)

        parts := split_by_char(mul_args_str, ',')
    
        if parts->astr_len() != 2 {
            continue
        }

        mul_result := 1
        valid := true

        for j := 0; j < parts->astr_len(); j += 1 {
            if !is_digit_str(parts[j]) {
                valid = false
                break 
            }

            value := stoi(parts[j], 10)
            mul_result *= value
        }
        
        if valid && mul_enabled {
            sum += mul_result
        }
    }

    printf("mul_count=%q\nvalid_count=%q\n", mul_count, valid_count)

    return sum
}

printf("%q\n", scan_for_muls(data))
