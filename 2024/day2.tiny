use array("str") as astr
use array("int") as aint

func split_by_char(input: str, ch: int): astr {
    // Necessary because mutating arguments doesn't work haha
    s := input
    parts := astr()

    while true {
        end_of_part_pos := strchr(s, ch)
        part := substr(s, 0, end_of_part_pos)

        astr_push(parts, part)

        if end_of_part_pos == -1 {
            break
        }
        
        s = substr(s, end_of_part_pos + 1, -1)
    }

    return parts
}

data := read_file("inputs/day2_example.txt")

lines := split_by_char(data, '\n')

safe_count := 0

func is_safe(nums: aint): bool {
    sign := 0
    prev_value := nums[0]

    for j := 1; j < aint_len(nums); j += 1 {
        value := nums[j]

        if sign == 0 {
            if prev_value < value {
                sign = 1
            } else {
                sign = -1
            }
        } else if sign < 0 {
            if prev_value < value {
                return false
            }
        } else {
            if prev_value > value {
                return false
            }
        }

        diff := value - prev_value
        if diff < 0 {
            diff *= -1
        }

        if diff < 1 || diff > 3 {
            return false
        }

        prev_value = value
    }

    return true
}

for i := 0; i < astr_len(lines); i += 1 {
    line := lines[i]

    num_strs := split_by_char(line, ' ')
    
    if astr_len(num_strs) < 2 {
        break
    }

    nums := aint()
    aint_resize(nums, astr_len(num_strs))

    for j := 0; j < astr_len(num_strs); j += 1 {
        nums[j] = str_to_int(num_strs[j])
    }

    if is_safe(nums) {
        safe_count += 1
    } 
}

printf("safe_reports=%q\n", safe_count)
